### A Pluto.jl notebook ###
# v0.12.18

using Markdown
using InteractiveUtils

# â•”â•â•¡ bf1385da-4ac2-11eb-3992-41abac921370
using Plots

# â•”â•â•¡ e97e5984-4ab9-11eb-3efb-9f54c6c307dd
# edit the code below to set your name and UGent username

student = (name = "Hanne Janssen", email = "Jeanette.Janssen@UGent.be");

# press the â–¶ button in the bottom right of this cell to run your edits
# or use Shift+Enter

# you might need to wait until all other cells in this notebook have completed running. 
# scroll down the page to see what's up

# â•”â•â•¡ f089cbaa-4ab9-11eb-09d1-05f49911487f
begin 
	using DSJulia;
	tracker = ProgressTracker(student.name, student.email);
	md"""

	Submission by: **_$(student.name)_**
	"""
end

# â•”â•â•¡ fd21a9fa-4ab9-11eb-05e9-0d0963826b9f
md"""
# Notebook 1: Getting up and running

First of all, **_welcome to the course, $(student[:name])!_**
"""

# â•”â•â•¡ 0f47f5b2-4aba-11eb-2e5a-b10407e3f928


# â•”â•â•¡ 23d3c9cc-4abd-11eb-0cb0-21673effee6c
md"""## 1. The basics
*From zero to newbie.*
"""

# â•”â•â•¡ 62c3b076-4ab7-11eb-0cf2-25cdf7d2540d
md"""
Let's get started with the basics. Some mathematical operations, """

# â•”â•â•¡ 7bf5bdbe-4ab7-11eb-0d4b-c116e02cb9d9
1 + 2       # adding integers

# â•”â•â•¡ 83306610-4ab7-11eb-3eb5-55a465e0abb9
1.0 + 2.0   # adding floats

# â•”â•â•¡ 3fa0a15c-5008-11eb-13b5-a91b02c1eb2d
1 + 2.0     # adding a float to an integer...

# â•”â•â•¡ 83311b8a-4ab7-11eb-0067-e57ceabdfe9d
2 / 4       # standard division

# â•”â•â•¡ 833dbc66-4ab7-11eb-216d-f9900f95deb8
div(2, 4)   # Computes 2/4 truncated to an integer

# â•”â•â•¡ 8342c042-4ab7-11eb-2136-497fc9e1b9c4
2 Ã· 4       # looks nices but exactly the same!

# â•”â•â•¡ 834d4cbc-4ab7-11eb-1f1a-df05b0c00d66
7 % 3       # get the remainder of the integer division

# â•”â•â•¡ 8360ffac-4ab7-11eb-1162-f7a536eb0765
35 \ 7      # inverse division

# â•”â•â•¡ 8365cb3e-4ab7-11eb-05c0-85f51cc9b018
1 // 3      # fractions, gives result as a rational

# â•”â•â•¡ 8370eaf0-4ab7-11eb-1cd3-dfeec9341c4b
1//2 + 1//4

# â•”â•â•¡ 8383f104-4ab7-11eb-38a5-33e59b1591f6
'c'        # characters (unicode)

# â•”â•â•¡ 8387934a-4ab7-11eb-11b2-471b08d87b31
:symbol    # symbols, mostly used for macros

# â•”â•â•¡ 8c14cb9a-4ab7-11eb-0666-b1d4aca00f97
md"variable assignment"

# â•”â•â•¡ 93b5a126-4ab7-11eb-2f67-290ed869d44a
x = 2

# â•”â•â•¡ 962ae6d2-4ab7-11eb-14a2-c76a2221f544
Ï„ = 1 / 37  # unicode variable names are allowed

# â•”â•â•¡ 98d48302-4ab7-11eb-2397-710d0ae425f7
md"""

unicode! In most Julia editing environments, unicode math symbols can be typed when starting with a '\' and hitting '[TAB]'.

"""

# â•”â•â•¡ cee8a766-4ab7-11eb-2bc7-898df2c9b1ff
# type \alpha  and <TAB>

# â•”â•â•¡ e2c5b558-4ab7-11eb-09be-b354fc56cc6e
md"Operators are overrated."

# â•”â•â•¡ ec754104-4ab7-11eb-2a44-557e4304dd43
5x         # This works

# â•”â•â•¡ f23a2d2a-4ab7-11eb-1e26-bb2d1d19829f
md"But strings are quite essential,"

# â•”â•â•¡ fa836e88-4ab7-11eb-0ba6-5fc7372f32ab
mystery = "life, the universe and everything"

# â•”â•â•¡ 0138ef46-4ab8-11eb-1813-55594927d661
md"and string interpolation is performed with `$`."

# â•”â•â•¡ 0b73d66a-4ab8-11eb-06e9-bbe95285a69f
"The answer to $mystery is $(3*2*7)"

# â•”â•â•¡ 6b6eb954-4ab8-11eb-17f9-ef3445d359a3
md"""
Printing can be done with `println()` but in this notebook environment this does not seem to do much.
"""

# â•”â•â•¡ 94e3eb74-4ab8-11eb-1b27-573dd2f02b1d
println("The answer to $mystery is $(3*2*7)")

# â•”â•â•¡ 7592f8a2-4ac0-11eb-375c-61c915380eeb
md"... but take a look at the terminal window."

# â•”â•â•¡ abf00a78-4ab8-11eb-1063-1bf4905ca250
md"""
repetitions of strings can be done using the operators `*` and `^`.
This use of `*` and `^` makes sense by analogy with multiplication and exponentiation. Just as `4^3` is equivalent to `4*4*4`, we expect `"Spam"^3` to be the same as `"Spam"*"Spam"*"Spam"`, and it is.
"""

# â•”â•â•¡ be220a48-4ab8-11eb-1cd4-db99cd9db066
breakfast = "eggs"

# â•”â•â•¡ cadaf948-4ab8-11eb-3110-259768055e85
abetterbreakfast = "SPAM"

# â•”â•â•¡ cadb506e-4ab8-11eb-23ed-2d5f88fd30b0
breakfast * abetterbreakfast

# â•”â•â•¡ caf56346-4ab8-11eb-38f5-41336c5b45a7
breakfast * abetterbreakfast^3 * breakfast

# â•”â•â•¡ 046133a8-4ab9-11eb-0591-9de27d85bbca
md"""
Lots of handy Â´String`-operations are available in the standard library of Julia:
"""

# â•”â•â•¡ 0c8bc7f0-4ab9-11eb-1c73-b7ec002c4155
uppercase("This feels like shouting.")

# â•”â•â•¡ 0f8a311e-4ab9-11eb-1b64-cd62b65c49bf
findfirst("a", "banana")

# â•”â•â•¡ 0f8a5e94-4ab9-11eb-170b-cfec74d6ebbc
findfirst("na", "banana")

# â•”â•â•¡ 0f96fdd6-4ab9-11eb-0e33-2719394a66ba
findnext("na", "banana", 4)

# â•”â•â•¡ 1f255304-4ab9-11eb-34f1-270fd5a95256
md"Unlike `Strings`, a `Char` value represents a single character and is surrounded by single quotes."

# â•”â•â•¡ 34a18900-4ab9-11eb-17a0-1168dd9d06f9
'x'

# â•”â•â•¡ 39a0a328-4ab9-11eb-0f37-6717095b56aa
md"
The operator `âˆˆ` (\in TAB) is a boolean operator that takes a character and a string and returns true if the first appears in the second:"

# â•”â•â•¡ 4749f268-4ab9-11eb-15a7-579437e0bd20
'a' âˆˆ "banana"

# â•”â•â•¡ 5a9bbbe6-4aba-11eb-3652-43eb7891f437


# â•”â•â•¡ 6bdc8a5e-4aba-11eb-263c-df3af7afa517
QuestionBlock(;
	title=md"**Question 1: logical statements**",
	description = md"""
	
	Check the behaviour of the relational operators on strings.
	
	```julia
	"apples" == "pears"
	"apples" < "pears"
	"apples" < "Pears"
	```
	""",
	questions = [Question(;description=md"", validators = Bool[], status=md"")]
)

# â•”â•â•¡ a69ead46-4abc-11eb-3d1d-eb1c73f65150
md"All binary arithmetic and bitwise operators have an updating version that assigns the result of the operation back into the left operand. The updating version of the binary operator is formed by placing a, `=`, immediately after the operator."

# â•”â•â•¡ b482b998-4abc-11eb-36da-379010485bfa
let         # the `let`-environment creates a local workspace, x will only exist here.
	x = 1   # inplace update of x
	x += 2  # inplace update of x
	x += 2
end

# â•”â•â•¡ 07b103ae-4abd-11eb-311b-278d1e033642
md"Similarly to Matlab, when using the REPL, Julia will print the result of every statement by default. To suppress this behavious, just end the statement with a semicolon."

# â•”â•â•¡ 15f8b7fe-4abd-11eb-2777-8fc8bf9d342e
a = 10;  # not printed...

# â•”â•â•¡ efae58fa-5008-11eb-32fe-c3ae588d14f2
a  # ...but still defined

# â•”â•â•¡ 18f99e46-4abd-11eb-20a8-859cb1b12fe3
b = 20

# â•”â•â•¡ 3a7954da-4abd-11eb-3c5b-858054b4d06b
md"""## 2. Logical statements

*From zero to one.*
"""


# â•”â•â•¡ 8b17d538-4abd-11eb-0543-ab95c9548d6f
md"""**Boolean operators**

Julia uses `true` and `false` for Boolean variables.
"""

# â•”â•â•¡ 29d34e64-5009-11eb-3301-f729150e17b2
IğŸ’–Julia = true 

# â•”â•â•¡ 91a9d1a0-4abd-11eb-3337-71983f32b6ae
!true

# â•”â•â•¡ 942d4202-4abd-11eb-1f01-dfe3df40a5b7
!false

# â•”â•â•¡ 942dae0e-4abd-11eb-20a2-37d9c9882ba8
1 == 1

# â•”â•â•¡ 943d9850-4abd-11eb-1cbc-a1bef988c910
2 == 1

# â•”â•â•¡ 943de2ce-4abd-11eb-2410-31382ae9c74f
1 != 1

# â•”â•â•¡ 9460c03c-4abd-11eb-0d60-4d8aeb5b0c1d
2 != 1

# â•”â•â•¡ 946161f4-4abd-11eb-0ec5-df225dc140d0
1 < 10

# â•”â•â•¡ 947d143a-4abd-11eb-067d-dff955c90407
1 > 10

# â•”â•â•¡ 947fea8e-4abd-11eb-1d6a-2bc540f7a50e
2 <= 2

# â•”â•â•¡ 948eff10-4abd-11eb-36d0-5183e882a9e2
2 >= 2

# â•”â•â•¡ 948f5032-4abd-11eb-3d1c-7da4cb64521c
# Comparisons can be chained
1 < 2 < 3

# â•”â•â•¡ 94b520e6-4abd-11eb-3161-addf3b0e4f24
2 < 3 < 2

# â•”â•â•¡ 94b78322-4abd-11eb-3006-454548efd164
# Logical operators
true && true

# â•”â•â•¡ 94d28c80-4abd-11eb-08c0-717207e4c682
true || false

# â•”â•â•¡ 9fe6e1a2-4abd-11eb-0c39-458ce94265c0
md"Likewise, we have the Boolean logic operators `&&` (AND), `||` (OR) and `âŠ»` (XOR, exclusive or)."

# â•”â•â•¡ ae26ab9e-4abd-11eb-3270-33558dbdf663
true && true

# â•”â•â•¡ b08dc886-4abd-11eb-1807-096a7e6fd6f9
true && false

# â•”â•â•¡ b08e3a28-4abd-11eb-258a-a5a93b4b882c
true || false

# â•”â•â•¡ b0a8dfe0-4abd-11eb-167d-2fc3974c7c92
false || false

# â•”â•â•¡ b0a97e00-4abd-11eb-371c-e138aea17bb6
true âŠ» false

# â•”â•â•¡ b0ccc252-4abd-11eb-048b-4bec3750bbf1
true âŠ» true

# â•”â•â•¡ 60b066d8-5009-11eb-3b4c-8b8fa2f4831d
md"""
Chaining logic operators is frequently done in Julia as a short alternative for an `if` statement. The idea is if you use an `&&` statement, the second part is only evaluated if the first part is true! The inverse is true for `||`, where the second part is only evaluated if the first part is false."""

# â•”â•â•¡ b8b7c0f6-5009-11eb-1eb7-0be723a88863
let
	x = 12
	(x > 10) && (x -= 5)  # I am evaluated!
	(x > 4) || (x *= 2)   # I am not... 
	x
end

# â•”â•â•¡ 1c5975ec-4abe-11eb-0ff0-bfb2f03a520b
statements = [ missing,   #first statement
				missing,  # second statement
				missing]  # third statement

# â•”â•â•¡ bd446c42-4abd-11eb-0465-d9a61c48ff48
begin

qb1 = QuestionBlock(;
	title=md"**Question 2: advanced logical statements**",
	description = md"""
	
	Predict the outcomes of the following statements.
	
	```julia
	z, y = true, false

	(z || y) && !(y || y) # first

	(z âŠ» y) && (!z âŠ» !y)  # second

	(z || y) âŠ» (z && y)   # third
	```
	""",
	questions = [Question(validators = statements, description = md"Replace `missing` with the correct boolean (true, false) below.")]
)
	validate(qb1, tracker)
end

# â•”â•â•¡ 1c22b880-4abf-11eb-3f18-756c1198ccad
md"## 3. Control flow"

# â•”â•â•¡ 37086212-4abf-11eb-3ec9-7f8dae57121e
md"The `if`, `else`, `elseif`-statement is instrumental to any programming language./ Note that control flow is ended with an `end` statement. In constrast to Python, tabs are only for clarity but do not impact functionality."

# â•”â•â•¡ 489421d8-4abf-11eb-0d5e-fd779cc918a1
if 4 > 3
  'A'
elseif 3 > 4
  'B'
else
  'C'
end

# â•”â•â•¡ 6736dafe-4abf-11eb-1fce-0716d2b7f4a8
md"""
Julia allows for some very condense control flow structures.
y = `condition` ? `valueiftrue` : `valueiffalse`
"""

# â•”â•â•¡ 0c693c24-4ac0-11eb-2329-c743dcc5039d
clip(x) = missing

# â•”â•â•¡ 8933033a-4abf-11eb-1156-a53a5ee9152c
begin
   qb2 = QuestionBlock(;
	title=md"**Question 3: clipping exercise**",
	questions = [
		Question(;
			description=md"""
			Complete the clip function: $\max(0, \min(1, x))$ for a given $x$, without making use of the functions `min` and `max`.

			Open assignments always return `missing`. 
			""",
			validators= @safe[clip(-1)==0, clip(0.3)==0.3, clip(1.1)==1.0],
			#hints = [hint(md"You might need to compartment your code using brackets.")]
		),
		
			
		],
		
	);
	
	validate(qb2, tracker)
end

# â•”â•â•¡ 035a53ba-4ac1-11eb-3c34-b50a803b7b7d
md"Oh yeah! ğŸ‰ You defined your first function in Julia. More on this later."

# â•”â•â•¡ 2a5fca7c-4ac0-11eb-33a3-23d972ca27b8
md"## 4. Looping"

# â•”â•â•¡ 3896642a-4ac0-11eb-2c7c-4f376ab82217
characters = ["Harry", "Ron", "Hermione"]

# â•”â•â•¡ 3ef3faf8-4ac0-11eb-1965-fd23413e29f3
for char in characters
  println("Character $char")
end

# â•”â•â•¡ 4118016e-4ac0-11eb-18bf-5de326782c87
for (i, char) in enumerate(characters)
  println("$i. $char")
end

# â•”â•â•¡ 4119fbca-4ac0-11eb-1ea9-0bdd324214c5
pets = ["Hedwig", "Pig", "Crookhanks"]

# â•”â•â•¡ 4139bf3c-4ac0-11eb-2b63-77a513149351
for (char, pet) in zip(characters, pets)
  println("$char has $pet as a pet")
end

# â•”â•â•¡ a1d4127c-4ac0-11eb-116f-79c6ee58f524
md"Strings can also be looped"

# â•”â•â•¡ a93b28e6-4ac0-11eb-074f-a7b64f43a194
getme = "a shrubbery"

# â•”â•â•¡ ac35b796-4ac0-11eb-3bc5-5ff4350d5452
for letter in getme  # or, equivalently, use `âˆˆ` instead of `in` to look like a mathematician!
  println("$letter")
end

# â•”â•â•¡ b18e55ae-4ac0-11eb-1455-21b83b7c61d5
let 
	n = 16757676187786;
	while n > 1
	  println(n)
	  if iseven(n)
		n = div(n, 2)
	  else
		n = 3n + 1
	  end
	end
end

# â•”â•â•¡ ec4190a8-4ac0-11eb-0421-398f063775bb
md"(Mathematical note: [they got closer to cracking this one](https://www.quantamagazine.org/mathematician-terence-tao-and-the-collatz-conjecture-20191211/?mc_cid=a3adbffb9f&mc_eid=41ed2fca13).)"

# â•”â•â•¡ 7dce10be-500c-11eb-12cf-43f6989aae91
md"A useful trick when working with loops is using `break` and `continue` statements. If a `break` is encountered, the loop will immediately be exited. A `continue` statement will only skip the rest of the current iteration but will not halt the loop."

# â•”â•â•¡ bafbe0ea-500c-11eb-10be-75d1738d42a2
let
	i = 1
	while true  # infinite loop
		if i^2 â‰¥ 231
			break
		end
		i += 1
	end
	i  # first integer with a square larger than 231
end

# â•”â•â•¡ 2033383e-500d-11eb-3aea-cdc1e90dad2c
let
	s = 0
	for i in 1:100
		!isodd(i) && continue
		s += i
	end
	s # sum of all odd numbers between 1 and 100
end

# â•”â•â•¡ fdb67aba-4ac0-11eb-1d4a-c354de54baa9
md"""## 5. Functions
Julia puts the fun in functions. User-defined functions can be declared as follows,


"""

# â•”â•â•¡ 28f47a24-4ac1-11eb-271f-6b4de7311db3
function square(x)
  result = x * x
  return result
end

# â•”â•â•¡ 47338c78-4ac1-11eb-04d6-35c2361eaea6
md"A more condensed version of of `square(x)`."

# â•”â•â•¡ 463689b0-4ac1-11eb-1b0f-b7a239011c5c
s(x) = x * x

# â•”â•â•¡ 52bfff04-4ac1-11eb-1216-25eedd9184c3
md"Passing an array to a function that takes a single element as argument takes a special syntax. By putting a `.` before the brackets, the function is executed on all the elements of the Array. More on this in **Part2: collections**."

# â•”â•â•¡ 61846dae-4ac1-11eb-389a-4fbe3f6145b1
s([1, 2, 3, 4, 5])   # Multiplication is not defined for Arrays

# â•”â•â•¡ 6321ae1a-4ac1-11eb-04cb-33e939694874
s.([1, 2, 3, 4, 5])  # This is an elements-wise execution of s()

# â•”â•â•¡ 7b874424-4ac1-11eb-2d4e-0b4607559b8f
md"""Keyword arguments are defined using a semicolon in the back signature and a default value can be assigned. "Keywords" assigned before the semicolon are default values but their keywords are not ignored."""

# â•”â•â•¡ 86defe2a-4ac1-11eb-3c01-c5e671877212
safelog(x, offset=0.1; base=10) = log(x + offset) / log(base)

# â•”â•â•¡ 886512de-4ac1-11eb-00e1-73292ec23277
safelog(0)

# â•”â•â•¡ 88678820-4ac1-11eb-272e-0df61e418900
safelog(0, 0.01)

# â•”â•â•¡ 888dee1e-4ac1-11eb-264d-cd4a4f30f498
safelog(0, 0.01, base=2)

# â•”â•â•¡ 8acb086c-4ac1-11eb-1715-756fde34b38f
md"""When functions have a variable number of arguments, one can use the *slurping* `...` operator to denote a variable number of arguments. The argument will be treated as a collection. For example"""

# â•”â•â•¡ 944e1aaa-4ac1-11eb-0e23-41b1c5d0e889
function mymean(X...)
  m = zero(first(X))  # ensures to be the same type as x
  # m = 0.0  # alternative that is less tidy
  for x in X
	m += x
  end
  return m / length(X)
end

# â•”â•â•¡ 9d4e11be-4ac1-11eb-1fa0-13f1fe60c3bc
mymean(1, 3, 5)

# â•”â•â•¡ 9d514ef6-4ac1-11eb-25fc-ffaa2dcc9b02
mymean(1, 3, 5, 7)

# â•”â•â•¡ a0781222-4ac1-11eb-3425-d9b9603487f3
md"Similarly, the *splatting* operator can be used to split a collection into its individual elements."

# â•”â•â•¡ a6b95d62-4ac1-11eb-0c93-7fa0f6a120d5
c = [1.0, 3.0, 5.0];

# â•”â•â•¡ ab006064-4ac1-11eb-32be-6557b8d45f32
mymean(c...)

# â•”â•â•¡ b0603566-4ac1-11eb-17bc-3b63cd2aa1e9
md"""When unsure of what a function does, in the REPL the documentation can be viewed by adding a "?" in front of the function. Here, in the Pluto environment, put the cursor in the function of interest and open the documentation tab."""

# â•”â•â•¡ beafce06-4ac1-11eb-2431-1ffeba45716b
sort

# â•”â•â•¡ ec487488-4ac1-11eb-1be3-a93e41f78bf3
md"""
For a lot of standard Julia functions a in-place version is defined. In-place means that the function changes one of the input arguments of the function. As an example, `sort()` sorts and returns the array passed as argument, it does not change the original array. In contrast, `sort!()` is the inplace version of sort and directly sorts the array passed as argument.
"""

# â•”â•â•¡ f88fee6c-4ac1-11eb-1671-43493122f061
my_unsorted_list = [4, 5, 9, 7, 1, 9]

# â•”â•â•¡ fa759f92-4ac1-11eb-0d72-1f9d6d38a831
sort(my_unsorted_list)

# â•”â•â•¡ fa7ba458-4ac1-11eb-2ca1-59ff3c032b26
my_unsorted_list

# â•”â•â•¡ fa9b3266-4ac1-11eb-153a-87c6a1124890
sort!(my_unsorted_list)

# â•”â•â•¡ fa9d43b2-4ac1-11eb-33fc-a37503cedabf
my_unsorted_list

# â•”â•â•¡ 0dbe0c34-500e-11eb-2633-67d8dc6b24c8
md"""
Functions are first-class and work just like any other variable! For example, you can give a function as an input in another function. 

In some cases, you might want to define an **anonymous function**, without given them a name:"""

# â•”â•â•¡ 900da226-500e-11eb-3f1c-5582ed0cf749
afun = x -> 2x^2 - 2x + 4

# â•”â•â•¡ b24bee9a-500e-11eb-2130-45b78bbebdec
afun(4)  # can be used as a regular function

# â•”â•â•¡ c8cf6520-500e-11eb-2dd3-4507a3994a83
md"""
A particular powerful tool is creating a **pipeline**, i.e., using the output of a previous function as input of the next one. This can be done using the `|>` syntax."""

# â•”â•â•¡ 19402512-500f-11eb-3ea7-c3d1c52025d6
Ï€/4 |> sin |> inv 

# â•”â•â•¡ 6dc0eaae-500f-11eb-3051-37d870366201
md"The `.` syntax also works here and it is often useful to combiny with anonymous functions."

# â•”â•â•¡ 8b57c506-500f-11eb-3114-55785eb593a7
1:100 .|> (x->x^2) .|> inv |> sum |> (x->6x) |> sqrt  # poor man's pi

# â•”â•â•¡ fd171e0e-4ac1-11eb-09ea-337d17500149
md"Specific functions can be generated if you have more information on the input type.
This is called multiple dispatch.

The `::` operator attaches type annotations to expressions and variables.

How does the documentation for the function square look like after you executed the cell below?"

# â•”â•â•¡ 10e71260-4ac2-11eb-1069-55613ee7df0a
function square(x::Float64)
  println("using function with floats")
  result = x * x
  return result
end

# â•”â•â•¡ 3e433ab4-4ac1-11eb-2178-53b7220fa9ab
square(2)

# â•”â•â•¡ 46112c44-4ac1-11eb-2ad8-030406c7cf67
square(2.0)

# â•”â•â•¡ 461489fa-4ac1-11eb-0596-1d3bedb61778
square("ni")   # the multiplication of strings is defined as a concatenation

# â•”â•â•¡ 1c0230f8-4ac2-11eb-32aa-e7a4b2ae9cff
square(4)

# â•”â•â•¡ 226417c2-4ac2-11eb-2914-196461e2b40e
square(4.)

# â•”â•â•¡ 3daf4fa6-4ac2-11eb-0541-b98c2e97dfe4
md"More about types in the next section !"

# â•”â•â•¡ a777f624-4f2f-11eb-0595-432ea5115a2d
md"## Intermezzo: Types"

# â•”â•â•¡ abb7bf4e-4f2f-11eb-1dde-abf1cb0fb8b4
 md"""All Julia objects, both those already defined as well as those you might make yourself, have a type. The type system is the secret sauce, allowing Julia to be fast because code can be specialized for a particular combination of types. It is also supremely useful in conjunction with *multiple dispatch*, in which functions work differently depending on which types you feed into them. In one of the exercises at the end of this notebook you will get a taster of how multiple dispatch can be used. A function behaviour changes depending on the type of the arguments. Day 2 will mainly focus on the type system, so in what follows, a concise introduction is given to types."""

# â•”â•â•¡ b63533e8-4f2f-11eb-3811-5f43fcaa2a9e
begin
	number = 42
	str = "mice"
	n = 0.9
	A = [1 2; 3 4]
end;

# â•”â•â•¡ b730b7b8-4f2f-11eb-3216-613c6100fb9c
typeof(number)

# â•”â•â•¡ b99eb16c-4f2f-11eb-0f1e-5d2b1244689f
typeof(str)

# â•”â•â•¡ bc67fc50-4f2f-11eb-2209-bf35df0e8c27
typeof(n)

# â•”â•â•¡ bf40274a-4f2f-11eb-0086-d3502f67161b
typeof(A)

# â•”â•â•¡ c48e035c-4f2f-11eb-12c7-576aadca7511
md"**multiple dispatch**"

# â•”â•â•¡ c8435740-4f2f-11eb-3046-3d45d5e0e805
 md"multiple dispatch is a concept where the behaviour of a function can be specified differently according to the type of the input arguments. Let's us look at an example,"

# â•”â•â•¡ cacb7254-4f2f-11eb-1daa-1bc04678835c
begin 
	translate(xy::Array) = xy .+ [1 3]
	translate(s::String) = Markdown.parse("[Ctrl + click here](https://translate.google.com/?sl=en&tl=nl&text=$(s)&op=translate)")
	translate(anything::Any) = "I'm lost in translation..."
end

# â•”â•â•¡ cc48bc9a-4f2f-11eb-134c-71bd8a944943
translate(1.0)

# â•”â•â•¡ d9f28c04-4f2f-11eb-0255-1965fb8f07b5
translate([0.0 0.0])

# â•”â•â•¡ dc1dbe90-4f2f-11eb-05ce-c1fe46ae14dd
translate("Hi how are you?")

# â•”â•â•¡ de48a3f6-4f2f-11eb-314b-493546c37a21
 md"A great deal of time will be spend on the julia's time system in day 2. So do not worry if this still feels vague. Just **remember** that all objects have a type and the behaviour of a function can vary depending on the type of the input arguments."

# â•”â•â•¡ 6da71180-4ac2-11eb-1cac-410bd1cce70c
md"""## 6. Macros
Macros provide a method to include generated code in the final body of a program. It is a way of generating a new output expression, given an unevaluated input expression. When your Julia program runs, it first parses and evaluates the macro, and the processed code produced by the macro is eventually evaluated like an ordinary expression.

Some nifty basic macros are `@time` and `@show`. `@time` prints the cpu time and memory allocations of an expression.
"""

# â•”â•â•¡ 85b96ff0-4ac2-11eb-077f-cf4aad8a3c24
@time square(10)  #Don't forget that printing happens in the terminal windows

# â•”â•â•¡ a11c2898-4ac2-11eb-24d3-6f8060b5fd65
md"""The `@show` macro is often useful for debugging purposes. It displays both the expression to be evaluated and its result, finally returning the value of the result."""

# â•”â•â•¡ a686e67e-4ac2-11eb-228e-23524a3ddc59
@show 1 + 1

# â•”â•â•¡ d50cced2-500d-11eb-2dcc-21fc50825f43
md"We will provide more examples tomorrow!"

# â•”â•â•¡ ad156892-4ac2-11eb-3634-a3783231e5a1
md"""## 7. Plotting

Quite essential for scientific programming is the visualisation of the results. `Plots` is the Julia package that handles a lot of the visualisation. `rand(10)`, returns an array of 10 random floats between 0 and 1.
"""

# â•”â•â•¡ d779956a-4ac2-11eb-39de-4b3cecace452
md"""When loading in a package for the first time Julia will have to precompile this package, hence this step can take some time."""

# â•”â•â•¡ c7d2a048-4ac2-11eb-3902-b7c8505096ae
begin 
	plot(1:10, rand(10), label="first")
	plot!(1:10, rand(10), label="second") # adding to current figure using plot!

	scatter!([1:10], randn(10), label="scatter")

	xlabel!("x")
	ylabel!("f(x)")
	title!("My pretty Julia plot")
end

# â•”â•â•¡ cf35b2b2-4ac2-11eb-1ae6-5d3c108210df
plot(0:0.1:10, x -> sin(x) / x, xlabel="x", ylabel="sin(x)/x", color=:red, marker=:square, legend=:none) 
# notice the use of a symbol as an argument !

# â•”â•â•¡ d1010f88-4ac2-11eb-0fa9-0902fef0cf9f
contour(-5:0.1:5, -10:0.1:10, (x, y) -> 3x^2-4y^2 + x*y/6)

# â•”â•â•¡ 19e74adc-4ac3-11eb-239f-7b0132287466


# â•”â•â•¡ 0e63d722-4ac3-11eb-3740-d31b47a77912
md"""### 8. Exercises"""

# â•”â•â•¡ c34ede1c-4ad4-11eb-050f-bb07c5d19c1c
begin 
qb6 = QuestionBlock(;
	title=md"**Question 6: time is relative**",
	description = md"""
The function `time` returns the current Greenwich Mean Time in seconds since "the epoch", which is an arbitrary time used as a reference point. On UNIX systems, the epoch is 1 January 1970.
Write a script that reads the current time and converts it to a time of day in hours, minutes, and seconds, plus the number of days since the epoch.
	""",
	questions = [Question(validators = [], description = md"")]
)
	validate(qb6, tracker)
end

# â•”â•â•¡ 0c306fd8-4ad5-11eb-1a9f-2d3d1e838a77
function since_epoch()
	return days, hours, minutes, seconds
end

# â•”â•â•¡ e99d6b96-4ad5-11eb-2144-f97a97e71ae4
begin 
	
q7 = Question(;
	description=md"""
1. Write a function named `checkfermat` that takes four parameters ($a$, $b$, $c$ and $n$) and checks to see if Fermatâ€™s theorem holds. If $n$ is greater than 2 and $a^n + b^n == c^n$ the program should print, "Holy smokes, Fermat was wrong!" Otherwise the program should print, "No, that doesnâ€™t work.""",
	validators = []		
)
	
q8 = Question(;
	description=md"""
2. Write a function that prompts the user to input values for $a$, $b$, $c$ and $n$, converts them to integers, and uses checkfermat to check whether they violate Fermatâ€™s theorem.""",
	validators = []		
)
	
q9 = QuestionOptional{Easy}(;
	description=md"""
3. Can you write the code so that the functions in 4.1 and 4.2 have the same name?""",
	validators = []		
)
	
	
	
qb7 = QuestionBlock(;
	title=md"**Question 7: Fermat's Last Theorem**",
	description = md"""
Fermatâ€™s Last Theorem says that there are no positive integers $a$, $b$, and $c$ such that

$a^n + b^n = c^n$

for any value of $n$ greater than 2.""",
	questions = [q7, q8, q9],
	hints=[
			hint(md"check the functions `readline` and `parse`."),
			hint(md"You can write multiple function with the same name but with a different number of input arguments. So you can write a second version of `checkfermat` with no input arguments for a prompting version. Depending on the number and the type of the arguments, Julia will choose the appropriate version of `checkfermat`. This mechanism is called multiple dispatch, more on this further in the course.")
			])
	validate(qb7, tracker)
end

# â•”â•â•¡ f077c390-57fe-11eb-1ad9-31110b3dac39
md"""### 9. Extra exercises"""

# â•”â•â•¡ 42f24f58-4ac3-11eb-06b5-ebc015c17520
begin 
qb3 = QuestionBlock(;
	title=md"**Question 4: can you justify this?**",
	description = md"""
	
	Write a function named `rightjustify` that takes a string named `s` as a parameter and prints the string with enough leading spaces so that the last letter of the string is in column 70 of the display.

	Use string concatenation and repetition. Also, Julia provides a built-in function called `length`. Check what it does!

	""",
	questions = [Question(validators = statements, description = md"")]
)
	validate(qb3, tracker)
end

# â•”â•â•¡ 87871f34-4ad1-11eb-3903-93e3f63ea14a
function rightjustify()
	missing
end

# â•”â•â•¡ 448ef88e-4ad2-11eb-20d6-17a51d665ef9
function print_grid()
	missing
end

# â•”â•â•¡ 14d50ee8-4ad3-11eb-3b81-9138aec66207
function print_big_grid()
	missing
end

# â•”â•â•¡ 01eb4816-4ad2-11eb-3991-af76de0110c5
begin 
q4 = Question(validators = [print_grid() == "+ - - - - + - - - - +\n|         |         |\n|         |         |\n|         |         |\n|         |         |\n+ - - - - + - - - - +\n|         |         |\n|         |         |\n|         |         |\n|         |         |\n+ - - - - + - - - - +"], 
		description = md"")

oq1 = QuestionOptional{Easy}(validators = [print_big_grid() == "+ - - - - + - - - - + - - - - + - - - - +\n|         |         |         |         |\n|         |         |         |         |\n|         |         |         |         |\n|         |         |         |         |\n+ - - - - + - - - - + - - - - + - - - - +\n|         |         |         |         |\n|         |         |         |         |\n|         |         |         |         |\n|         |         |         |         |\n+ - - - - + - - - - + - - - - + - - - - +\n|         |         |         |         |\n|         |         |         |         |\n|         |         |         |         |\n|         |         |         |         |\n+ - - - - + - - - - + - - - - + - - - - +\n|         |         |         |         |\n|         |         |         |         |\n|         |         |         |         |\n|         |         |         |         |\n+ - - - - + - - - - + - - - - + - - - - +"], 
		description = md"Write a function that draws a similar grid with four rows and four columns.")
	
qb4 = QuestionBlock(;
	title=md"**Question 5: grid print**",
	description = md"""
	Complete the function `printgrid` that draws a grid like the following:
	```
	+ - - - - + - - - - +
	|         |         |
	|         |         |
	|         |         |
	|         |         |
	+ - - - - + - - - - +
	|         |         |
	|         |         |
	|         |         |
	|         |         |
	+ - - - - + - - - - +
	```

	""", questions = [q4, oq1], 
		hints=[
				hint(md"""The function `print` does not advance to the next line."""),
				hint(md"""To print more than one value on a line, you can print a comma-separated sequence of values:

`println("+", "-")`
""")
			]
	)
	validate(qb4, tracker)
end

# â•”â•â•¡ Cell order:
# â•Ÿâ”€f089cbaa-4ab9-11eb-09d1-05f49911487f
# â• â•e97e5984-4ab9-11eb-3efb-9f54c6c307dd
# â• â•fd21a9fa-4ab9-11eb-05e9-0d0963826b9f
# â•Ÿâ”€0f47f5b2-4aba-11eb-2e5a-b10407e3f928
# â• â•23d3c9cc-4abd-11eb-0cb0-21673effee6c
# â• â•62c3b076-4ab7-11eb-0cf2-25cdf7d2540d
# â• â•7bf5bdbe-4ab7-11eb-0d4b-c116e02cb9d9
# â• â•83306610-4ab7-11eb-3eb5-55a465e0abb9
# â• â•3fa0a15c-5008-11eb-13b5-a91b02c1eb2d
# â• â•83311b8a-4ab7-11eb-0067-e57ceabdfe9d
# â• â•833dbc66-4ab7-11eb-216d-f9900f95deb8
# â• â•8342c042-4ab7-11eb-2136-497fc9e1b9c4
# â• â•834d4cbc-4ab7-11eb-1f1a-df05b0c00d66
# â• â•8360ffac-4ab7-11eb-1162-f7a536eb0765
# â• â•8365cb3e-4ab7-11eb-05c0-85f51cc9b018
# â• â•8370eaf0-4ab7-11eb-1cd3-dfeec9341c4b
# â• â•8383f104-4ab7-11eb-38a5-33e59b1591f6
# â• â•8387934a-4ab7-11eb-11b2-471b08d87b31
# â• â•8c14cb9a-4ab7-11eb-0666-b1d4aca00f97
# â• â•93b5a126-4ab7-11eb-2f67-290ed869d44a
# â• â•962ae6d2-4ab7-11eb-14a2-c76a2221f544
# â• â•98d48302-4ab7-11eb-2397-710d0ae425f7
# â• â•cee8a766-4ab7-11eb-2bc7-898df2c9b1ff
# â• â•e2c5b558-4ab7-11eb-09be-b354fc56cc6e
# â• â•ec754104-4ab7-11eb-2a44-557e4304dd43
# â• â•f23a2d2a-4ab7-11eb-1e26-bb2d1d19829f
# â• â•fa836e88-4ab7-11eb-0ba6-5fc7372f32ab
# â• â•0138ef46-4ab8-11eb-1813-55594927d661
# â• â•0b73d66a-4ab8-11eb-06e9-bbe95285a69f
# â• â•6b6eb954-4ab8-11eb-17f9-ef3445d359a3
# â• â•94e3eb74-4ab8-11eb-1b27-573dd2f02b1d
# â• â•7592f8a2-4ac0-11eb-375c-61c915380eeb
# â• â•abf00a78-4ab8-11eb-1063-1bf4905ca250
# â• â•be220a48-4ab8-11eb-1cd4-db99cd9db066
# â• â•cadaf948-4ab8-11eb-3110-259768055e85
# â• â•cadb506e-4ab8-11eb-23ed-2d5f88fd30b0
# â• â•caf56346-4ab8-11eb-38f5-41336c5b45a7
# â• â•046133a8-4ab9-11eb-0591-9de27d85bbca
# â• â•0c8bc7f0-4ab9-11eb-1c73-b7ec002c4155
# â• â•0f8a311e-4ab9-11eb-1b64-cd62b65c49bf
# â• â•0f8a5e94-4ab9-11eb-170b-cfec74d6ebbc
# â• â•0f96fdd6-4ab9-11eb-0e33-2719394a66ba
# â• â•1f255304-4ab9-11eb-34f1-270fd5a95256
# â• â•34a18900-4ab9-11eb-17a0-1168dd9d06f9
# â• â•39a0a328-4ab9-11eb-0f37-6717095b56aa
# â• â•4749f268-4ab9-11eb-15a7-579437e0bd20
# â• â•5a9bbbe6-4aba-11eb-3652-43eb7891f437
# â• â•6bdc8a5e-4aba-11eb-263c-df3af7afa517
# â• â•a69ead46-4abc-11eb-3d1d-eb1c73f65150
# â• â•b482b998-4abc-11eb-36da-379010485bfa
# â• â•07b103ae-4abd-11eb-311b-278d1e033642
# â• â•15f8b7fe-4abd-11eb-2777-8fc8bf9d342e
# â• â•efae58fa-5008-11eb-32fe-c3ae588d14f2
# â• â•18f99e46-4abd-11eb-20a8-859cb1b12fe3
# â• â•3a7954da-4abd-11eb-3c5b-858054b4d06b
# â• â•8b17d538-4abd-11eb-0543-ab95c9548d6f
# â• â•29d34e64-5009-11eb-3301-f729150e17b2
# â• â•91a9d1a0-4abd-11eb-3337-71983f32b6ae
# â• â•942d4202-4abd-11eb-1f01-dfe3df40a5b7
# â• â•942dae0e-4abd-11eb-20a2-37d9c9882ba8
# â• â•943d9850-4abd-11eb-1cbc-a1bef988c910
# â• â•943de2ce-4abd-11eb-2410-31382ae9c74f
# â• â•9460c03c-4abd-11eb-0d60-4d8aeb5b0c1d
# â• â•946161f4-4abd-11eb-0ec5-df225dc140d0
# â• â•947d143a-4abd-11eb-067d-dff955c90407
# â• â•947fea8e-4abd-11eb-1d6a-2bc540f7a50e
# â• â•948eff10-4abd-11eb-36d0-5183e882a9e2
# â• â•948f5032-4abd-11eb-3d1c-7da4cb64521c
# â• â•94b520e6-4abd-11eb-3161-addf3b0e4f24
# â• â•94b78322-4abd-11eb-3006-454548efd164
# â• â•94d28c80-4abd-11eb-08c0-717207e4c682
# â•Ÿâ”€9fe6e1a2-4abd-11eb-0c39-458ce94265c0
# â• â•ae26ab9e-4abd-11eb-3270-33558dbdf663
# â• â•b08dc886-4abd-11eb-1807-096a7e6fd6f9
# â• â•b08e3a28-4abd-11eb-258a-a5a93b4b882c
# â• â•b0a8dfe0-4abd-11eb-167d-2fc3974c7c92
# â• â•b0a97e00-4abd-11eb-371c-e138aea17bb6
# â• â•b0ccc252-4abd-11eb-048b-4bec3750bbf1
# â• â•60b066d8-5009-11eb-3b4c-8b8fa2f4831d
# â• â•b8b7c0f6-5009-11eb-1eb7-0be723a88863
# â• â•bd446c42-4abd-11eb-0465-d9a61c48ff48
# â• â•1c5975ec-4abe-11eb-0ff0-bfb2f03a520b
# â• â•1c22b880-4abf-11eb-3f18-756c1198ccad
# â• â•37086212-4abf-11eb-3ec9-7f8dae57121e
# â• â•489421d8-4abf-11eb-0d5e-fd779cc918a1
# â• â•6736dafe-4abf-11eb-1fce-0716d2b7f4a8
# â• â•8933033a-4abf-11eb-1156-a53a5ee9152c
# â• â•0c693c24-4ac0-11eb-2329-c743dcc5039d
# â• â•035a53ba-4ac1-11eb-3c34-b50a803b7b7d
# â• â•2a5fca7c-4ac0-11eb-33a3-23d972ca27b8
# â• â•3896642a-4ac0-11eb-2c7c-4f376ab82217
# â• â•3ef3faf8-4ac0-11eb-1965-fd23413e29f3
# â• â•4118016e-4ac0-11eb-18bf-5de326782c87
# â• â•4119fbca-4ac0-11eb-1ea9-0bdd324214c5
# â• â•4139bf3c-4ac0-11eb-2b63-77a513149351
# â• â•a1d4127c-4ac0-11eb-116f-79c6ee58f524
# â• â•a93b28e6-4ac0-11eb-074f-a7b64f43a194
# â• â•ac35b796-4ac0-11eb-3bc5-5ff4350d5452
# â• â•b18e55ae-4ac0-11eb-1455-21b83b7c61d5
# â• â•ec4190a8-4ac0-11eb-0421-398f063775bb
# â• â•7dce10be-500c-11eb-12cf-43f6989aae91
# â• â•bafbe0ea-500c-11eb-10be-75d1738d42a2
# â• â•2033383e-500d-11eb-3aea-cdc1e90dad2c
# â• â•fdb67aba-4ac0-11eb-1d4a-c354de54baa9
# â• â•28f47a24-4ac1-11eb-271f-6b4de7311db3
# â• â•3e433ab4-4ac1-11eb-2178-53b7220fa9ab
# â• â•46112c44-4ac1-11eb-2ad8-030406c7cf67
# â• â•461489fa-4ac1-11eb-0596-1d3bedb61778
# â• â•47338c78-4ac1-11eb-04d6-35c2361eaea6
# â• â•463689b0-4ac1-11eb-1b0f-b7a239011c5c
# â•Ÿâ”€52bfff04-4ac1-11eb-1216-25eedd9184c3
# â• â•61846dae-4ac1-11eb-389a-4fbe3f6145b1
# â• â•6321ae1a-4ac1-11eb-04cb-33e939694874
# â• â•7b874424-4ac1-11eb-2d4e-0b4607559b8f
# â• â•86defe2a-4ac1-11eb-3c01-c5e671877212
# â• â•886512de-4ac1-11eb-00e1-73292ec23277
# â• â•88678820-4ac1-11eb-272e-0df61e418900
# â• â•888dee1e-4ac1-11eb-264d-cd4a4f30f498
# â• â•8acb086c-4ac1-11eb-1715-756fde34b38f
# â• â•944e1aaa-4ac1-11eb-0e23-41b1c5d0e889
# â• â•9d4e11be-4ac1-11eb-1fa0-13f1fe60c3bc
# â• â•9d514ef6-4ac1-11eb-25fc-ffaa2dcc9b02
# â• â•a0781222-4ac1-11eb-3425-d9b9603487f3
# â• â•a6b95d62-4ac1-11eb-0c93-7fa0f6a120d5
# â• â•ab006064-4ac1-11eb-32be-6557b8d45f32
# â• â•b0603566-4ac1-11eb-17bc-3b63cd2aa1e9
# â• â•beafce06-4ac1-11eb-2431-1ffeba45716b
# â• â•ec487488-4ac1-11eb-1be3-a93e41f78bf3
# â• â•f88fee6c-4ac1-11eb-1671-43493122f061
# â• â•fa759f92-4ac1-11eb-0d72-1f9d6d38a831
# â• â•fa7ba458-4ac1-11eb-2ca1-59ff3c032b26
# â• â•fa9b3266-4ac1-11eb-153a-87c6a1124890
# â• â•fa9d43b2-4ac1-11eb-33fc-a37503cedabf
# â• â•0dbe0c34-500e-11eb-2633-67d8dc6b24c8
# â• â•900da226-500e-11eb-3f1c-5582ed0cf749
# â• â•b24bee9a-500e-11eb-2130-45b78bbebdec
# â• â•c8cf6520-500e-11eb-2dd3-4507a3994a83
# â• â•19402512-500f-11eb-3ea7-c3d1c52025d6
# â• â•6dc0eaae-500f-11eb-3051-37d870366201
# â• â•8b57c506-500f-11eb-3114-55785eb593a7
# â• â•fd171e0e-4ac1-11eb-09ea-337d17500149
# â• â•10e71260-4ac2-11eb-1069-55613ee7df0a
# â• â•1c0230f8-4ac2-11eb-32aa-e7a4b2ae9cff
# â• â•226417c2-4ac2-11eb-2914-196461e2b40e
# â• â•3daf4fa6-4ac2-11eb-0541-b98c2e97dfe4
# â• â•a777f624-4f2f-11eb-0595-432ea5115a2d
# â• â•abb7bf4e-4f2f-11eb-1dde-abf1cb0fb8b4
# â• â•b63533e8-4f2f-11eb-3811-5f43fcaa2a9e
# â• â•b730b7b8-4f2f-11eb-3216-613c6100fb9c
# â• â•b99eb16c-4f2f-11eb-0f1e-5d2b1244689f
# â• â•bc67fc50-4f2f-11eb-2209-bf35df0e8c27
# â• â•bf40274a-4f2f-11eb-0086-d3502f67161b
# â• â•c48e035c-4f2f-11eb-12c7-576aadca7511
# â• â•c8435740-4f2f-11eb-3046-3d45d5e0e805
# â• â•cacb7254-4f2f-11eb-1daa-1bc04678835c
# â• â•cc48bc9a-4f2f-11eb-134c-71bd8a944943
# â• â•d9f28c04-4f2f-11eb-0255-1965fb8f07b5
# â• â•dc1dbe90-4f2f-11eb-05ce-c1fe46ae14dd
# â• â•de48a3f6-4f2f-11eb-314b-493546c37a21
# â• â•6da71180-4ac2-11eb-1cac-410bd1cce70c
# â• â•85b96ff0-4ac2-11eb-077f-cf4aad8a3c24
# â• â•a11c2898-4ac2-11eb-24d3-6f8060b5fd65
# â• â•a686e67e-4ac2-11eb-228e-23524a3ddc59
# â• â•d50cced2-500d-11eb-2dcc-21fc50825f43
# â• â•ad156892-4ac2-11eb-3634-a3783231e5a1
# â• â•bf1385da-4ac2-11eb-3992-41abac921370
# â• â•d779956a-4ac2-11eb-39de-4b3cecace452
# â• â•c7d2a048-4ac2-11eb-3902-b7c8505096ae
# â• â•cf35b2b2-4ac2-11eb-1ae6-5d3c108210df
# â• â•d1010f88-4ac2-11eb-0fa9-0902fef0cf9f
# â• â•19e74adc-4ac3-11eb-239f-7b0132287466
# â• â•0e63d722-4ac3-11eb-3740-d31b47a77912
# â• â•c34ede1c-4ad4-11eb-050f-bb07c5d19c1c
# â• â•0c306fd8-4ad5-11eb-1a9f-2d3d1e838a77
# â• â•e99d6b96-4ad5-11eb-2144-f97a97e71ae4
# â• â•f077c390-57fe-11eb-1ad9-31110b3dac39
# â• â•42f24f58-4ac3-11eb-06b5-ebc015c17520
# â• â•87871f34-4ad1-11eb-3903-93e3f63ea14a
# â• â•01eb4816-4ad2-11eb-3991-af76de0110c5
# â• â•448ef88e-4ad2-11eb-20d6-17a51d665ef9
# â• â•14d50ee8-4ad3-11eb-3b81-9138aec66207
